@component-name = "portal-feature-flags"
definition {

	property custom.properties = "feature.flag.LPS-162766=false${line.separator}feature.flag.LPS-162766.type=beta${line.separator}feature.flag.LPS-162766.title=Terms and Conditions pages${line.separator}feature.flag.LPS-162766.description=Create your own terms and conditions pages${line.separator}feature.flag.LPS-162766.dependencies=LPS-162765";
	property portal.acceptance = "true";
	property testray.main.component.name = "Feature Flags";
	property testray.component.names = "Feature Flags";

	setUp {
		TestCase.setUpPortalInstance();

		User.firstLoginPG();

		task ("Given the Feature Flags page") {
			ApplicationsMenu.gotoPortlet(
				category = "Configuration",
				panel = "Control Panel",
				portlet = "Instance Settings");

			Click(
				key_itemName = "Feature Flags",
				locator1 = "ListGroupItem#ITEM_TEXT");
		}
	}

	tearDown {
		var testLiferayVirtualInstance = PropsUtil.get("test.liferay.virtual.instance");

		if (${testLiferayVirtualInstance} == "true") {
			PortalInstances.tearDownCP();
		}
	}

	@description = "LPS-167698 - This tests asserts that a feature flag can be Disabled"
	@priority = 5
	test CanBeDisabled {
		task ("And a feature flag is enabled") {
			if (IsElementPresent(locator1 = "FeatureFlags#TOGGLE_ROW", title_row = "Terms and Conditions pages", toggle_state = "Disabled")) {
				Check.checkToggleSwitch(
					locator1 = "FeatureFlags#TOGGLE_ROW",
					title_row = "Terms and Conditions pages",
					toggle_state = "Disabled");
			}
		}

		task ("When the administrator disables a feature flag") {
			Check.checkToggleSwitch(
				locator1 = "FeatureFlags#TOGGLE_ROW",
				title_row = "Terms and Conditions pages",
				toggle_state = "Enabled");
		}

		task ("Then assert the toggle is disabled state") {
			AssertElementPresent(
				locator1 = "FeatureFlags#TOGGLE_ROW",
				title_row = "Terms and Conditions pages",
				toggle_state = "Disabled");
		}
	}

	@description = "LPS-167698 - This tests asserts that a feature flag can be enabled"
	@priority = 5
	test CanBeEnabled {
		task ("And a feature flag is disabled") {
			if (IsElementPresent(locator1 = "FeatureFlags#TOGGLE_ROW", title_row = "Terms and Conditions pages", toggle_state = "Enabled")) {
				Check.checkToggleSwitch(
					locator1 = "FeatureFlags#TOGGLE_ROW",
					title_row = "Terms and Conditions pages",
					toggle_state = "Enabled");
			}
		}

		task ("When the administrator enables a feature flag") {
			Check.checkToggleSwitch(
				locator1 = "FeatureFlags#TOGGLE_ROW",
				title_row = "Terms and Conditions pages",
				toggle_state = "Disabled");
		}

		task ("Then assert the toggle is enabled state") {
			AssertElementPresent(
				locator1 = "FeatureFlags#TOGGLE_ROW",
				title_row = "Terms and Conditions pages",
				toggle_state = "Enabled");
		}
	}

}