definition {

	macro addTestraySite {
		Site.openSitesAdmin();

		LexiconEntry.gotoAdd();

		LexiconCard.clickCard(card = "Liferay Site Initializer Testray");

		SelectFrame(
			key_title = "Add Site",
			locator1 = "IFrame#MODAL_ANY");

		PortletEntry.inputName(name = ${siteName});

		Button.click(button = "Add");

		Site.openSitesAdmin();

		while (IsElementNotPresent(key_siteName = ${siteName}, locator1 = "Sites#SITE_TABLE_NAME")) {
			Refresh();

			WaitForPageLoad();
		}
	}

	macro addTR2Site {
		Site.openSitesAdmin();

		Site.addSiteTemplateCP(
			siteName = ${siteName},
			siteTemplateName = "Liferay Site Initializer Testray");
	}

	macro assertItemsPerPageValue {
		AssertElementPresent(
			key_value = ${itemValue},
			locator1 = "Testray#ITEMS_PER_PAGE_VALUE_BUTTON");
	}

	macro assertNameTitle {
		AssertElementPresent(
			key_title = ${key_name},
			locator1 = "Testray#NAME_TITLE");
	}

	macro assertPageNumber {
		AssertElementPresent(
			key_pageNumber = ${pageNumber},
			locator1 = "Pagination#ANY_PAGE");
	}

	macro assertProjectDescription {
		AssertElementPresent(
			key_value = ${value},
			locator1 = "Testray#DESCRIPTION_PROJECT");
	}

	macro assertProjectTitleHeader {
		AssertElementPresent(
			key_projectName = ${projectName},
			locator1 = "Testray#PROJECT_NAME_HEADER");
	}

	macro assertShowedItems {
		AssertElementPresent(
			key_text = ${itemsText},
			locator1 = "Testray#ITEMS_PER_PAGE_DESCRIPTION");
	}

	macro backSectionBreadcrumb {
		for (var n : list "1,2,3,4") {
			KeyPress(
				locator1 = "TestrayTestflow#BREADCRUMB_EDIT",
				value1 = "\BACK_SPACE");
		}

		if (isSet(value)) {
			Type(
				locator1 = "TestrayTestflow#BREADCRUMB_EDIT",
				value1 = ${value});

			AssertTextEquals(
				locator1 = "TestrayTestflow#BREADCRUMB_EDIT",
				value1 = ${value});
		}
	}

	macro changePagination {
		WaitForElementPresent(locator1 = "Pagination#RESULTS");

		if (${modal} == "true") {
			Click(locator1 = "Testray#ITEMS_PER_PAGE_SELECT_MODAL");

			Click(
				key_quantity = ${quantity},
				locator1 = "Testray#PAGINATION_ITEMS_MODAL");
		}
		else {
			Click(locator1 = "Pagination#ITEMS_PER_PAGE_SELECT");

			Click(
				key_quantity = ${quantity},
				locator1 = "Testray#PAGINATION_ITEMS");
		}
	}

	macro checkSuccessAlertAppearsAndDisappears {
		WaitForElementPresent(locator1 = "Message#SUCCESS");

		Alert.closeMessage(messageType = "SUCCESS");

		WaitForElementNotPresent(locator1 = "Message#SUCCESS");
	}

	macro clickBreadcrumb {
		var key_text = ${text};

		Click(locator1 = "Testray#BREADCRUMB_ENTRY");
	}

	macro clickModalButton {
		Click(
			key_name = ${button},
			locator1 = "Button#GENERAL_BUTTON_MODAL_FOOTER");

		WaitForLiferayEvent.initializeLiferayEventLog();
	}

	macro clickTableEntry {
		WaitForPageLoad();

		Click(
			key_tableEntry = ${tableEntry},
			locator1 = "Testray#TABLE_ENTRY");
	}

	macro closeModal {
		SelectFrameTop();

		var key_modalTitle = ${modalTitle};

		Click(locator1 = "Button#CLOSE_MODAL");
	}

	macro confirmBreadcrumb {
		KeyPress.keyPressNoError(
			locator1 = "TestrayTestflow#BREADCRUMB_EDIT",
			value1 = "\ENTER");
	}

	macro createProject {
		Click(locator1 = "Button#PLUS");

		SelectFrame(locator1 = "Testray#MODAL_BODY");

		PortletEntry.inputName(name = ${projectName});

		Type(
			locator1 = "Testray#NEW_DESCRIPTION_PROJECT",
			value1 = ${description});

		Button.clickSave();
	}

	macro createProjectByDropdown {
		TestrayDropdown.selectOptionSidebar(
			buttonOption = "Manage",
			menuItem = "Add Project");

		PortletEntry.inputName(name = ${projectName});

		Type(
			locator1 = "Testray#NEW_DESCRIPTION_PROJECT",
			value1 = ${description});

		Button.clickSave();
	}

	macro deleteAllProjects {
		TestrayDropdown.goToResultSidebar();

		Refresh();

		Testray.changePagination(quantity = 150);

		while (IsElementPresent(key_tableEntry = "Projects_", locator1 = "Testray#TABLE_ENTRY")) {
			TestrayManage.selectTableRowOption(
				rowOption = "Delete",
				tableEntry = "Projects_");

			AssertConfirm(value1 = "Are you sure you want to delete this item?");

			Alert.viewSuccessMessage();
		}
	}

	macro deleteItem {
		TestrayManage.selectTableRowOption(
			rowOption = "Delete",
			tableEntry = ${itemName});

		AssertConfirm(value1 = "Are you sure you want to delete this item?");

		Alert.viewSuccessMessage();
	}

	macro deleteProject {
		TestrayDropdown.goToResultSidebar();

		Testray.changePagination(quantity = 150);

		TestrayManage.selectTableRowOption(
			rowOption = "Delete",
			tableEntry = ${projectName});

		AssertConfirm(value1 = "Are you sure you want to delete this item?");
	}

	macro editProject {
		SelectFrame(locator1 = "Testray#MODAL_BODY");

		PortletEntry.inputName(name = ${newName});

		Type(
			locator1 = "Testray#NEW_DESCRIPTION_PROJECT",
			value1 = ${newDescription});

		Button.clickSave();
	}

	macro gotoAddProject {
		Click(locator1 = "Testray#ADD_PROJECT");
	}

	macro gotoBreadcrumb {
		KeyPress(
			locator1 = "//body",
			value1 = "/");
	}

	macro goToLink {
		WaitForPageLoad();

		Click.clickAtNotVisible(
			key_text = ${text},
			locator1 = "Testray#LINK_TO_HOME");
	}

	macro goToNextAndPreviousPage {
		Click(
			key_pageDirection = ${pageDirection},
			locator1 = "Testray#NEXT_AND_PREVIOUS_PAGE_BUTTON");
	}

	macro kebabMenuOption {
		Click(locator1 = "ObjectAdmin#KEBAB_MENU");

		ClickNoError(
			key_kebabOption = ${kebabOption},
			locator1 = "ObjectAdmin#KEBAB_MENU_OPTION");
	}

	macro nextSectionBreadcrumb {
		if (isSet(project)) {
			Type(
				locator1 = "TestrayTestflow#BREADCRUMB_EDIT",
				value1 = ${project});

			AssertTextEquals(
				locator1 = "TestrayTestflow#BREADCRUMB_EDIT",
				value1 = ${project});
		}

		if (isSet(project)) {
			KeyPress(
				locator1 = "TestrayTestflow#BREADCRUMB_EDIT",
				value1 = "\TAB");
		}

		if (isSet(routine)) {
			Type(
				locator1 = "TestrayTestflow#BREADCRUMB_EDIT",
				value1 = ${routine});

			AssertTextEquals(
				locator1 = "TestrayTestflow#BREADCRUMB_EDIT",
				value1 = ${routine});
		}

		if (isSet(routine)) {
			KeyPress(
				locator1 = "TestrayTestflow#BREADCRUMB_EDIT",
				value1 = "\TAB");
		}

		if (isSet(build)) {
			Type(
				locator1 = "TestrayTestflow#BREADCRUMB_EDIT",
				value1 = ${build});

			AssertTextEquals(
				locator1 = "TestrayTestflow#BREADCRUMB_EDIT",
				value1 = ${build});
		}

		if (isSet(build)) {
			KeyPress(
				locator1 = "TestrayTestflow#BREADCRUMB_EDIT",
				value1 = "\TAB");
		}

		if (isSet(run)) {
			Type(
				locator1 = "TestrayTestflow#BREADCRUMB_EDIT",
				value1 = ${run});

			AssertTextEquals(
				locator1 = "TestrayTestflow#BREADCRUMB_EDIT",
				value1 = ${run});
		}

		if (isSet(run)) {
			KeyPress(
				locator1 = "TestrayTestflow#BREADCRUMB_EDIT",
				value1 = "\TAB");
		}

		if (isSet(caseResult)) {
			Type(
				locator1 = "TestrayTestflow#BREADCRUMB_EDIT",
				value1 = ${caseResult});

			AssertTextEquals(
				locator1 = "TestrayTestflow#BREADCRUMB_EDIT",
				value1 = ${caseResult});
		}

		Testray.confirmBreadcrumb();
	}

	macro pageNumberNavigation {
		Click(
			key_pageNumber = ${pageNumber},
			locator1 = "Pagination#ANY_PAGE");
	}

	macro pageNumberNavigationModal {
		Click(
			key_pageNumber = ${pageNumber},
			locator1 = "Testray#PAGE_NUMBER_MODAL");
	}

	macro selectCheckboxOptions {
		WaitForElementPresent(
			key_text = ${linkText},
			locator1 = "Button#ANY");

		Click(
			key_text = ${linkText},
			locator1 = "Button#ANY");

		if (isSet(checkboxNameList)) {
			for (var checkboxNameList : list ${checkboxNameList}) {
				Check.checkNotVisible(
					key_checkboxOption = ${checkboxNameList},
					locator1 = "Checkbox#SPECIFIC_CHECKBOX_IN_TABLE");
			}
		}

		if (isSet(uncheckboxNameList)) {
			for (var uncheckboxNameList : list ${uncheckboxNameList}) {
				Uncheck.uncheckNotVisible(
					key_checkboxOption = ${uncheckboxNameList},
					locator1 = "Checkbox#SPECIFIC_CHECKBOX_IN_TABLE");
			}
		}

		Click(
			key_text = ${buttonText},
			locator1 = "Button#ANY");
	}

	macro selectDefaultEnvironmentFactors {
		AssertElementPresent.pauseAssertVisible(
			key_arrowDirection = "Right",
			key_caseParameters = "Available",
			locator1 = "Testray#ARROW_DIRECTION");

		if (${arrowDirection} == "Right") {
			for (var listboxItem : list ${listCategory}) {
				Click(
					key_availableType = ${listboxItem},
					key_caseParameters = "Available",
					locator1 = "Testray#PARAMETER_TYPE_OPTIONS",
					value1 = ${listboxItem});

				Click(
					key_arrowDirection = ${arrowDirection},
					key_caseParameters = "Available",
					locator1 = "Testray#ARROW_DIRECTION");
			}
		}

		if (${arrowDirection} == "Left") {
			for (var listboxItem : list ${listCategory}) {
				Click(
					key_availableType = ${listboxItem},
					key_caseParameters = "Selected",
					locator1 = "Testray#PARAMETER_TYPE_OPTIONS",
					value1 = ${listboxItem});

				Click(
					key_arrowDirection = ${arrowDirection},
					key_caseParameters = "Selected",
					locator1 = "Testray#ARROW_DIRECTION");
			}
		}

		Button.click(button = "Next");

		if (isSet(firstOption)) {
			Select(
				key_fieldLabel = "Category 1",
				locator1 = "Select#GENERIC_SELECT_FIELD",
				value1 = ${firstOption});
		}

		if (isSet(secondOption)) {
			Select(
				key_fieldLabel = "Category 2",
				locator1 = "Select#GENERIC_SELECT_FIELD",
				value1 = ${secondOption});
		}

		if (isSet(applicationServer)) {
			Select(
				key_fieldLabel = "Application Server",
				locator1 = "Select#GENERIC_SELECT_FIELD",
				value1 = ${applicationServer});
		}

		if (isSet(browser)) {
			Select(
				key_fieldLabel = "Browser",
				locator1 = "Select#GENERIC_SELECT_FIELD",
				value1 = ${browser});
		}

		if (isSet(database)) {
			Select(
				key_fieldLabel = "Database",
				locator1 = "Select#GENERIC_SELECT_FIELD",
				value1 = ${database});
		}

		if (isSet(javaJDK)) {
			Select(
				key_fieldLabel = "Java JDK",
				locator1 = "Select#GENERIC_SELECT_FIELD",
				value1 = ${javaJDK});
		}

		if (isSet(liferayVersion)) {
			Select(
				key_fieldLabel = "Liferay Version",
				locator1 = "Select#GENERIC_SELECT_FIELD",
				value1 = ${liferayVersion});
		}

		if (isSet(mobileBrowser)) {
			Select(
				key_fieldLabel = "Mobile Browser",
				locator1 = "Select#GENERIC_SELECT_FIELD",
				value1 = ${mobileBrowser});
		}

		if (isSet(mobileDevice)) {
			Select(
				key_fieldLabel = "Mobile Device",
				locator1 = "Select#GENERIC_SELECT_FIELD",
				value1 = ${mobileDevice});
		}

		if (isSet(mobileOperatingSystem)) {
			Select(
				key_fieldLabel = "Mobile Operating System",
				locator1 = "Select#GENERIC_SELECT_FIELD",
				value1 = ${mobileOperatingSystem});
		}

		if (isSet(operatingSystem)) {
			Select(
				key_fieldLabel = "Operating System",
				locator1 = "Select#GENERIC_SELECT_FIELD",
				value1 = ${operatingSystem});
		}

		if (isSet(portalEdition)) {
			Select(
				key_fieldLabel = "Portal Edition",
				locator1 = "Select#GENERIC_SELECT_FIELD",
				value1 = ${portalEdition});
		}

		if (isSet(productEdition)) {
			Select(
				key_fieldLabel = "Product Edition",
				locator1 = "Select#GENERIC_SELECT_FIELD",
				value1 = ${productEdition});
		}

		Button.click(button = "Save");
	}

	macro selectUsers {
		Button.click(button = "Assign Users");

		for (var userName : list ${userNameList}) {
			Check.checkToggleSwitch(
				key_checkboxOption = ${userName},
				locator1 = "Checkbox#SPECIFIC_CHECKBOX_IN_TABLE");
		}

		Button.click(button = "Select Users");
	}

	macro signOutTestray {
		Click(locator1 = "TestrayUser#USER_ICON_SIDEBAR");

		Click(locator1 = "TestrayUser#SIGN_OUT");
	}

	macro tearDownCP {
		ApplicationsMenu.gotoPortlet(
			category = "Sites",
			panel = "Control Panel",
			portlet = "Sites");

		Refresh();

		PortletEntry.selectAll();

		ClickNoError(locator1 = "Icon#DELETE");

		Click(locator1 = "Button#MODAL_DELETE");

		Alert.viewSuccessMessage();
	}

	macro testrayApisTearDown {
		Testray.tearDownCP();

		ClientExtensionGeneral.goToRemoteAppsPortlet();

		ClientExtensionGeneral.deleteRemoteApp(tableEntry = "Testray");
	}

	macro typeProjectsField {
		SelectFrame(locator1 = "Testray#MODAL_BODY");

		PortletEntry.inputName(name = ${projectName});

		Type(
			locator1 = "Testray#NEW_DESCRIPTION_PROJECT",
			value1 = ${description});

		Button.clickSave();
	}

	macro verifySpecificEnvironmentFactors {
		if (isSet(listCategory)) {
			for (var listboxItem : list ${listCategory}) {
				AssertElementPresent(
					key_environmentFactors = ${listboxItem},
					locator1 = "Testray#OPTIONS_ENVIRONMENT_FACTORS_SELECTED");
			}
		}

		Button.click(button = "Next");

		if (isSet(listCategory)) {
			for (var listboxItem : list ${listCategory}) {
				AssertElementPresent(
					key_fieldLabel = ${listCategory},
					locator1 = "Select#GENERIC_SELECT_FIELD",
					value1 = ${listOption});
			}
		}
	}

	macro viewAlert {
		var key_alertText = ${alertText};

		AssertVisible(locator1 = "CustomerPortalSite#ALERT_NOTIFICATIONS");
	}

	macro viewErrorMessage {
		AssertElementPresent(
			key_fieldLabel = ${fieldLabel},
			key_warningMessage = ${warningMessage},
			locator1 = "Testray#FIELD_REQUIRED_MESSAGE");
	}

	macro viewOverviewTab {
		AssertElementPresent(
			key_rowValue = ${rowValue},
			locator1 = "Testray#OVERVIEW_LAYOUT_VALUES");
	}

	macro viewTableDetails {
		for (var detailList : list ${detailsList}) {
			AssertElementPresent(
				key_text = ${detailList},
				locator1 = "TestrayBuild#TABLE_DETAILS");
		}
	}

	macro viewTableEntryList {
		for (var tableEntry : list ${listEntry}) {
			AssertElementPresent(
				key_tableEntry = ${tableEntry},
				locator1 = "Testray#TABLE_ENTRY");
		}
	}

	macro viewTableEntryListNotPresent {
		for (var tableEntry : list ${listEntry}) {
			AssertElementNotPresent(
				key_tableEntry = ${tableEntry},
				locator1 = "Testray#TABLE_ENTRY");
		}
	}

}