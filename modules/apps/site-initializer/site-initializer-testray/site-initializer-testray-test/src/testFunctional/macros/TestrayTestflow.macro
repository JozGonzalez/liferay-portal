definition {

	macro addTask {
		Click(locator1 = "Button#PLUS");

		TestrayTestflow.editBreadcrumb(
			build = ${selectBuild},
			project = ${selectProject},
			routine = ${selectRoutine});

		Button.click(button = "Analyze");

		if (isSet(taskName)) {
			PortletEntry.inputName(name = ${taskName});
		}

		if (isSet(checkCaseType)) {
			Click(
				key_checkboxOption = ${checkCaseType},
				locator1 = "Checkbox#ENABLE_CHECKBOX_VIA_SPAN");
		}

		Button.click(button = "Assign Users");

		if (isSet(userList)) {
			for (var userName : list ${userList}) {
				Check.checkToggleSwitch(
					key_checkboxOption = ${userName},
					locator1 = "Checkbox#SPECIFIC_CHECKBOX_IN_TABLE");
			}
		}

		Button.click(button = "Select Users");

		Testray.checkSuccessAlertAppearsAndDisappears();

		Button.click(button = "Save");
	}

	macro assertAssignTask {
		AssertElementPresent(
			key_email = ${email},
			key_userName = ${userName},
			locator1 = "TestrayTestflow#ASSIGN_INFORMATIONS");
	}

	macro assertStatus {
		AssertElementPresent(
			key_label = ${label},
			key_status = ${status},
			locator1 = "TestrayTestflow#TESTFLOW_STATUS");
	}

	macro assertTaskDetails {
		AssertElementPresent(
			key_build = ${build},
			key_project = ${project},
			key_routine = ${routine},
			key_status = ${status},
			key_taskName = ${taskName},
			locator1 = "TestrayTestflow#TASK_DETAILS");
	}

	macro assignUsers {
		if (isSet(assignButton)) {
			Button.click(button = ${assignButton});

			SelectFrame(locator1 = "Testray#MODAL_BODY");

			Testray.clickTableEntry(tableEntry = ${userName});

			Alert.viewSuccessMessage();
		}
		else {
			TestrayManage.selectTableRowOption(
				rowOption = ${rowOption},
				tableEntry = ${tableEntry});

			SelectFrame(locator1 = "Testray#MODAL_BODY");

			Testray.clickTableEntry(tableEntry = ${userName});

			Alert.viewSuccessMessage();
		}
	}

	macro editBreadcrumb {
		Click(locator1 = "TestrayTestflow#BREADCRUMB_EDIT");

		Type(
			locator1 = "TestrayTestflow#BREADCRUMB_EDIT",
			value1 = ${project});

		if (isSet(project)) {
			Click(locator1 = "TestrayTestflow#BREADCRUMB_SELECT_OPTIONS");
		}

		Type(
			locator1 = "TestrayTestflow#BREADCRUMB_EDIT",
			value1 = ${routine});

		if (isSet(routine)) {
			Click(locator1 = "TestrayTestflow#BREADCRUMB_SELECT_OPTIONS");
		}

		Type(
			locator1 = "TestrayTestflow#BREADCRUMB_EDIT",
			value1 = ${build});

		if (isSet(build)) {
			Click(locator1 = "TestrayTestflow#BREADCRUMB_SELECT_OPTIONS");
		}
	}

	macro editStatus {
		WaitForElementPresent(
			key_text = ${button},
			locator1 = "Button#ANY");

		Button.click(button = ${button});

		WaitForPageLoad();

		Select(
			locator1 = "Select#SELECT_ID",
			selectFieldId = "dueStatus",
			value1 = ${selectStatus});

		if (isSet(issues)) {
			PortletEntry.inputText(
				fieldLabel = "Issues",
				text = ${issues});
		}

		if (isSet(comment)) {
			Type(
				key_fieldId = "comment",
				locator1 = "TextArea#ANY_ID",
				value1 = ${comment});
		}

		Button.click(button = "Save");
	}

	macro editStatusByRightClick {
		TestrayManage.selectTableRowOption(
			rowOption = "Complete",
			tableEntry = ${tableEntry});

		WaitForPageLoad();

		Select(
			locator1 = "Select#SELECT_ID",
			selectFieldId = "dueStatus",
			value1 = ${selectStatus});

		if (isSet(issues)) {
			PortletEntry.inputText(
				fieldLabel = "Issues",
				text = ${issues});
		}

		if (isSet(comment)) {
			Type(
				key_fieldId = "comment",
				locator1 = "TextArea#ANY_ID",
				value1 = ${comment});
		}

		Button.click(button = "Save");
	}

	macro editTask {
		Button.click(button = "Edit Task");

		if (isSet(taskName)) {
			PortletEntry.inputName(name = ${taskName});
		}

		if (isSet(userList)) {
			Button.click(button = "Assign Users");

			for (var userName : list ${userList}) {
				Check.checkToggleSwitch(
					key_checkboxOption = ${userName},
					locator1 = "Checkbox#SPECIFIC_CHECKBOX_IN_TABLE");
			}

			Button.click(button = "Select Users");

			Testray.checkSuccessAlertAppearsAndDisappears();
		}

		Button.click(button = "Save");
	}

	macro goToTestflow {
		Click(
			key_button = "TestFlow",
			locator1 = "TestrayDropdown#SIDEBAR_CONTENT");
	}

	macro taskOptions {
		if (isSet(taskName)) {
			PortletEntry.inputName(name = ${taskName});
		}

		Button.click(button = "Save");
	}

	macro userAvatarNotPresent {
		AssertElementNotPresent(locator1 = "TestrayTestflow#USER_AVATAR_IMAGE");
	}

	macro userAvatarPresent {
		AssertElementPresent(locator1 = "TestrayTestflow#USER_AVATAR_IMAGE");
	}

}