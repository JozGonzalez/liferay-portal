definition {

	macro assertStatusBuildDisabled {
		AssertElementPresent(
			key_item = ${key_item},
			locator1 = "TestrayRoutines#STATUS_BUILD_HISTORY_DISABLED");
	}

	macro clickStatusBuildHistory {
		Click(
			key_item = ${key_item},
			locator1 = "TestrayRoutines#STATUS_BUILD_HISTORY");
	}

	macro createRoutine {
		Click(locator1 = "Testray#ADD_PROJECT");

		Type(
			locator1 = "TestrayManage#NEW_OPTION_NAME",
			value1 = ${routineName});

		Click(locator1 = "TestrayManage#SAVE_BUTTON");
	}

	macro deleteRoutineSetup {
		TestrayDropdown.goToResultSidebar();

		Refresh();

		Testray.changePagination(quantity = 150);

		if (IsElementPresent(key_tableEntry = "Projects_", locator1 = "Testray#TABLE_ENTRY")) {
			Testray.clickTableEntry(tableEntry = "Projects_1 Project");
		}

		if (IsElementPresent(key_tableEntry = "Routines_1 Routine", locator1 = "Testray#TABLE_ENTRY")) {
			Testray.deleteItem(itemName = "Routines_1 Routine");

			Alert.viewSuccessMessage();

			Testray.checkSuccessAlertAppearsAndDisappears();
		}
	}

	macro editRoutine {
		TestrayManage.selectTableRowOption(
			rowOption = "Edit",
			tableEntry = ${tableEntry});

		Type(
			locator1 = "TestrayManage#NEW_OPTION_NAME",
			value1 = ${newName});

		Click(locator1 = "TestrayManage#SAVE_BUTTON");
	}

	macro selectDefaultEnvironmentFactors {
		Testray.kebabMenuOption(kebabOption = "Select Default Environment Factors");

		WaitForElementPresent(
			key_environmentFactors = "Application Server",
			locator1 = "Testray#SELECT_ENVIRONMENT_FACTORS");

		for (var listboxItem : list ${listCategory}) {
			Click(
				key_environmentFactors = ${listboxItem},
				locator1 = "Testray#SELECT_ENVIRONMENT_FACTORS");

			Click(locator1 = "Testray#AUDIT_GRAPH_CONFIGURATION_RIGHT_ARROW_BUTTON");
		}

		Button.click(button = "Next");

		Select(
			key_fieldLabel = ${categoryField},
			locator1 = "Select#GENERIC_SELECT_FIELD",
			value1 = ${listOption});

		if (isSet(categoryField2)) {
			Select(
				key_fieldLabel = ${categoryField2},
				locator1 = "Select#GENERIC_SELECT_FIELD",
				value1 = ${listOption2});
		}

		Button.click(button = "Save");
	}

	macro selectOptionDropdownRoutine {
		Click(locator1 = "TestrayRoutines#DROPDOWN_ROUTINE");

		MenuItem.clickNoError(menuItem = ${menuItem});

		Alert.viewSuccessMessage();
	}

	macro typeRoutineField {
		Type(
			locator1 = "TestrayManage#NEW_OPTION_NAME",
			value1 = ${key_name});

		Click(locator1 = "TestrayManage#SAVE_BUTTON");
	}

}