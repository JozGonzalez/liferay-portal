@component-name = "portal-solutions"
definition {

	property custom.properties = "feature.flag.LPS-163118=true";
	property osgi.modules.includes = "site-initializer-testray";
	property portal.release = "false";
	property portal.upstream = "true";
	property release.feature.flags.disable.DISABLE_PRIVATE_LAYOUTS = "true";
	property testray.main.component.name = "Site Initializer Testray";

	var projectName = "Project 1";
	var testSiteName = "Testray 2";

	setUp {
		task ("Set up instance and sign in") {
			TestCase.setUpPortalInstance();

			User.firstLoginPG();
		}

		task ("Create a new Testray Site") {
			HeadlessSite.addSite(
				siteName = ${testSiteName},
				templateName = "Testray",
				templateType = "Site Initializer");
		}

		task ("Go to Testray Site") {
			ApplicationsMenu.gotoSite(site = ${testSiteName});
		}
	}

	tearDown {
		task ("Delete project via API, Testray Site and Client Extensions") {
			JSONTestrayAPI.deleteProject(projectName = ${projectName});

			Testray.testrayApisTearDown();
		}

		var testLiferayVirtualInstance = PropsUtil.get("test.liferay.virtual.instance");

		if (${testLiferayVirtualInstance} == "true") {
			PortalInstances.tearDownCP();
		}
	}

	@description = "Story ID: LPS-165583 | Test Summary: Verify if as an Administrator I am able to add a Project"
	@priority = 5
	test AdminCanAddProject {
		property test.liferay.virtual.instance = "false";
		property test.run.type = "single";

		task ("When the user Create a Project") {
			Testray.createProject(
				description = "Project Description",
				projectName = "Project 1");
		}

		task ("Then can see the project in the list") {
			Testray.changePagination(quantity = 150);

			Testray.viewTableEntryList(listEntry = "Project 1");
		}
	}

	@description = "Story ID: LPS-146927 | Test Summary: Verify that as an Administrator I am able to add a Project from the dropdown"
	@priority = 3
	test AdminCanAddProjectFromDropdown {
		property test.liferay.virtual.instance = "false";
		property test.run.type = "single";

		task ("When the user Create a Project from the dropdown") {
			Testray.createProjectByDropdown(
				description = "Project Description",
				projectName = "Project 1");
		}

		task ("Then can see the project in the list") {
			Testray.changePagination(quantity = 150);

			Testray.viewTableEntryList(listEntry = "Project 1");
		}
	}

	@description = "Story ID: LPS-146927 | Test Summary: Verify if as an Administrator I am able to delete a Project"
	@priority = 5
	test AdminCanDeleteProject {
		property test.liferay.virtual.instance = "false";
		property test.run.type = "single";

		task ("Given that the user Create a Project via API") {
			JSONTestray.addProject(
				projectDescription = "Project Description 1",
				projectName = ${projectName});
		}

		task ("When the project is deleted") {
			Testray.deleteProject(projectName = "Project 1");
		}

		task ("Then the project is not displayed anymore") {
			Testray.viewTableEntryListNotPresent(listEntry = "Project 1");
		}
	}

	@description = "Story ID: LPS-146927 | Test Summary: Verify if as an Administrator I am able to edit a Project"
	@priority = 5
	test AdminCanEditProject {
		property test.liferay.virtual.instance = "false";
		property test.run.type = "single";

		task ("Given that the user Create a Project") {
			Testray.createProject(
				description = "Project Description",
				projectName = "Project one");
		}

		task ("When the user edits the created project") {
			Testray.changePagination(quantity = 150);

			TestrayManage.selectTableRowOption(
				rowOption = "Edit",
				tableEntry = "Project one");

			Testray.editProject(
				newDescription = "Project Description Edited",
				newName = "Project 1");

			Testray.changePagination(quantity = 150);
		}

		task ("Then the project are presented with details edited") {
			Testray.viewTableEntryList(listEntry = "Project 1");
		}
	}

	@description = "Story ID: LPS-146927 | Test Summary: Verify if as an Administrator I should not be able to add a duplicate Project"
	@priority = 3
	test AdminCanNotAddDuplicateProject {
		property test.liferay.virtual.instance = "false";
		property test.run.type = "single";

		task ("When the user tries to create a duplicate project") {
			JSONTestray.addProject(
				projectDescription = "Project Description 1",
				projectName = ${projectName});

			Testray.createProject(
				description = "Project Description",
				projectName = "Project 1");
		}

		task ("Then an error message is displayed") {
			Alert.viewErrorMessage(errorMessage = "The Project name already exists");
		}
	}

	@description = "Story ID: LPS-146927 | Test Summary: Verify that the user can not create a project if all the required fields are not filled in"
	@priority = 2
	test AdminCanViewWarningMessage {
		property test.liferay.virtual.instance = "false";
		property test.run.type = "single";

		task ("When the user tries to create a project without a name") {
			Testray.createProject(
				description = "",
				projectName = "");
		}

		task ("Then an error message is displayed") {
			Testray.viewErrorMessage(
				fieldLabel = "Name",
				warningMessage = "name is a required field");
		}
	}

	@description = "Story ID: LPS-146927 | Test Summary: Verify if as an Administrator I should be able to delete a project that contains routines without associated builds"
	@priority = 3
	test CanDeleteProjectsThatContainRoutinesWithoutAssociatedBuilds {
		property test.liferay.virtual.instance = "false";
		property test.run.type = "single";

		task ("Given that the user Create a Project without associated builds") {
			JSONTestray.addProject(
				projectDescription = "Project Description 1",
				projectName = ${projectName});

			Testray.changePagination(quantity = 150);

			Testray.clickTableEntry(tableEntry = ${projectName});

			TestrayRoutines.createRoutine(routineName = "Routine 1");
		}

		task ("When the project is deleted") {
			Testray.deleteProject(projectName = "Project 1");
		}

		task ("Then the project is not displayed anymore") {
			Testray.viewTableEntryListNotPresent(listEntry = "Project 1");
		}
	}

	@description = "Story ID: LPS-146927 | Test Summary: Verify if as an Administrator I should not be able to delete a project that contains routines with associated builds"
	@priority = 4
	test CannotDeleteProjectsThatContainRoutinesWithAssociatedBuilds {
		property test.liferay.virtual.instance = "false";
		property test.run.type = "single";

		task ("Given that the user Create a Project") {
			JSONTestray.addProject(
				projectDescription = "Project Description 1",
				projectName = ${projectName});

			Testray.changePagination(quantity = 150);
		}

		task ("And add a new team") {
			TestrayManage.selectTableRowOption(
				rowOption = "Manage Teams",
				tableEntry = ${projectName});

			TestrayManage.createTeam(teamName = "Components_1 Team");

			Testray.closeModal(modalTitle = "Teams - Project 1");
		}

		task ("And add a component") {
			TestrayManage.selectTableRowOption(
				rowOption = "Manage Components",
				tableEntry = ${projectName});

			TestrayManage.createComponent(
				componentName = "Components_1 Project 1",
				teamName = "Components_1 Team");

			Refresh();
		}

		task ("And create a new Routine and a new Case") {
			Testray.changePagination(quantity = 150);

			Testray.clickTableEntry(tableEntry = ${projectName});

			TestrayRoutines.createRoutine(routineName = "Routines_1 Routine");

			Navigator.gotoNavTab(navTab = "Cases");

			TestrayCases.createCases(
				caseName = "Cases_1 Case",
				caseType = "Manual Test",
				component = "Components_1 Project 1",
				description = "Cases_1 Case Description",
				duration = 40,
				priority = 5,
				steps = "Auto Test Steps");

			Navigator.gotoNavTab(navTab = "Routines");
		}

		task ("And add a new build with enviroments") {
			TestrayManage.selectTableRowOption(
				rowOption = "Select Default Environment Factors",
				tableEntry = "Routines_1 Routine");

			TestrayBuild.selectDefaultEnvironmentFactors(
				label = "Application Server",
				label1 = "Browser",
				listCategory = "Application Server,Browser",
				listOption = "Tomcat 6.0",
				listOption1 = "Chrome 57");

			Testray.clickTableEntry(tableEntry = "Routines_1 Routine");

			TestrayBuild.addBuildViaUI(
				addVersion = "1.2",
				buildFieldName = "Builds_1 Project",
				caseList = "Cases_1 Case",
				description = "Builds_1 Project Description",
				dropdownItem = "New Build",
				gitHash = 12345,
				productVersionOption = "1.2",
				routine = "Routines_1 Routine");

			Testray.viewTableEntryList(listEntry = "Builds_1 Project");
		}

		task ("When the user tries to delete a project with associated constructions") {
			Testray.deleteProject(projectName = ${projectName});
		}

		task ("Then an error message is displayed") {
			Alert.viewErrorMessage(errorMessage = "The Project cannot be deleted because it has associated builds");
		}
	}

}