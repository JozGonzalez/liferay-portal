@component-name = "portal-solutions"
definition {

	property custom.properties = "feature.flag.LPS-163118=true";
	property osgi.modules.includes = "site-initializer-testray";
	property portal.release = "false";
	property portal.upstream = "true";
	property release.feature.flags.disable.DISABLE_PRIVATE_LAYOUTS = "true";
	property testray.main.component.name = "Site Initializer Testray";

	var testSiteName = "Testray 2";

	setUp {
		task ("Set up instance and sign in") {
			TestCase.setUpPortalInstance();

			User.firstLoginPG();
		}

		task ("Create a new Testray Site") {
			HeadlessSite.addSite(
				siteName = ${testSiteName},
				templateName = "Testray",
				templateType = "Site Initializer");
		}

		task ("Add a user via API") {
			JSONUser.addUser(
				userEmailAddress = "user@liferay.com",
				userFirstName = "userfn",
				userLastName = "userln",
				userScreenName = "usersn");
		}

		task ("Go to Testray") {
			ApplicationsMenu.gotoSite(site = ${testSiteName});
		}
	}

	tearDown {
		var testLiferayVirtualInstance = PropsUtil.get("test.liferay.virtual.instance");

		task ("Delete user via API, Testray Site and Client Extensions") {
			JSONUser.deleteUserByEmailAddress(userEmailAddress = "user@liferay.com");

			Testray.testrayApisTearDown();
		}

		if (${testLiferayVirtualInstance} == "true") {
			PortalInstances.tearDownCP();
		}
	}

	@description = "Story ID: LPS-173531 | Test Summary: Verify that as an administrator can add and delete a new user"
	@priority = 5
	test AdminCanAddAndDeleteNewUser {
		task ("Given that go to manage user") {
			TestrayDropdown.selectOptionSidebar(
				buttonOption = "Manage",
				menuItem = "Manage Users");
		}

		task ("When the administrator add a user") {
			Click(locator1 = "Button#PLUS");

			TestrayManageUser.addUser(
				listRoles = "Testray User",
				rePassword = "test1",
				userEmailAddressEdit = "userfirst@liferay.com",
				userFirstNameEdit = "changedfirstname",
				userLastNameEdit = "Updateln",
				userPassword = "test1",
				userScreenNameEdit = "testscreen");
		}

		task ("Then can see the user created in the table") {
			Testray.viewTableEntryList(listEntry = "changedfirstname Updateln");
		}

		task ("And delete the user") {
			Testray.clickTableEntry(tableEntry = "changedfirstname Updateln");

			Button.clickNoError(button = "Delete User");

			AssertConfirm(value1 = "Are you sure you want to delete this item?");
		}

		task ("Then can not see the user in the table") {
			Refresh();

			Testray.viewTableEntryListNotPresent(listEntry = "changedfirstname Updateln");
		}
	}

	@description = "Story ID: LPS-173531 | Test Summary: Verify if as an Administrator I should be able to filter manage users by name"
	@priority = 4
	test AdminCanFilterUsersByName {
		task ("Given that go to manage user") {
			TestrayDropdown.selectOptionSidebar(
				buttonOption = "Manage",
				menuItem = "Manage Users");
		}

		task ("And assert that have two users present in the table") {
			Testray.viewTableEntryList(listEntry = "userfn userln,Test Test");
		}

		task ("When the user filter a user by name") {
			TestrayFilter.clickFilter();

			TestrayFilter.typeFilter(
				label = "Name",
				value = "userfn userln");
		}

		task ("Then can see the user named test test in the table") {
			Testray.viewTableEntryList(listEntry = "userfn userln");
		}

		task ("And can not longer see the userfn userln in the table") {
			Testray.viewTableEntryListNotPresent(listEntry = "Test Test");
		}
	}

	@description = "Story ID: LPS-173531 | Test Summary: Verify if as an Administrator I should not see manage users that don't exist when I try filtering for them"
	@priority = 3
	test AdminCanNotFilterForNonExistentUsers {
		task ("Given that go to manage user") {
			TestrayDropdown.selectOptionSidebar(
				buttonOption = "Manage",
				menuItem = "Manage Users");
		}

		task ("And assert that the user is present") {
			Testray.viewTableEntryList(listEntry = "Test Test");
		}

		task ("When filtering a user by his non-existent name") {
			TestrayFilter.clickFilter();

			TestrayFilter.typeFilter(
				label = "Name",
				value = "Test 2");
		}

		task ("Then the user is not present in the table") {
			Testray.viewTableEntryListNotPresent(listEntry = "Test Test");
		}
	}

	@description = "Story ID: LPS-149551 | Test Summary: Verify if as an User can edit account information"
	@priority = 4
	test CanEditAccountInformation {
		task ("Given that go to user created") {
			TestrayDropdown.selectOptionSidebar(
				buttonOption = "Manage",
				menuItem = "Manage Users");

			Testray.clickTableEntry(tableEntry = "user@liferay.com");
		}

		task ("When the user change the account informations") {
			TestrayManageUser.editUserInformation(
				userEmailAddressEdit = "user@liferay.com",
				userFirstNameEdit = "changedfirstname",
				userLastNameEdit = "Updateln",
				userScreenNameEdit = "testscreen");

			Testray.checkSuccessAlertAppearsAndDisappears();
		}

		task ("And go to user changed") {
			Testray.clickTableEntry(tableEntry = "changedfirstname Updateln");
		}

		task ("Then can see the new informations to the account") {
			TestrayManageUser.viewUserInfomation(
				emailAddress = "user@liferay.com",
				firstName = "changedfirstname",
				lastName = "Updateln",
				screenName = "testscreen");
		}
	}

	@description = "Story ID: LPS-164964 | Test Summary: Verify if as an administrator I cannot save roles without the select options"
	@priority = 3
	test CanNotSaveRolesWithoutOptions {
		task ("Given that go to manage user") {
			TestrayDropdown.selectOptionSidebar(
				buttonOption = "Manage",
				menuItem = "Manage Users");

			Testray.clickTableEntry(tableEntry = "test@liferay.com");
		}

		task ("And the user deselects all roles") {
			TestrayManageUser.changeRole(roleName = "Administrator");

			TestrayManageUser.changeRole(roleName = "Power User");

			TestrayManageUser.changeRole(roleName = "User");
		}

		task ("When the user tries to save roles without options selected") {
			Button.clickSave();
		}

		task ("Then the error message appear") {
			Alert.viewErrorMessage(errorMessage = "Please, select one or more roles.");
		}
	}

	@description = "Story ID: LPS-149551 | Test Summary: Verify that the user can not edit informations of user if all the required fields are not filled in"
	@priority = 2
	test CanViewFieldRequired {
		task ("Given that go to manage user") {
			TestrayDropdown.selectOptionSidebar(
				buttonOption = "Manage",
				menuItem = "Manage Users");

			Testray.clickTableEntry(tableEntry = "user@liferay.com");
		}

		task ("When the user tries to created account without informations") {
			TestrayManageUser.editUserInformation(
				userEmailAddressEdit = "",
				userFirstNameEdit = "",
				userLastNameEdit = "",
				userScreenNameEdit = "");
		}

		task ("Then a warning message is displayed in the required fields") {
			Testray.viewErrorMessage(
				fieldLabel = "First Name",
				warningMessage = "This field is required.");

			Testray.viewErrorMessage(
				fieldLabel = "Last Name",
				warningMessage = "This field is required.");

			Testray.viewErrorMessage(
				fieldLabel = "Email Address",
				warningMessage = "This field is required.");

			Testray.viewErrorMessage(
				fieldLabel = "Screen Name",
				warningMessage = "This field is required.");
		}
	}

}