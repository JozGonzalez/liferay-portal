@component-name = "portal-solutions"
definition {

	property custom.properties = "feature.flag.LPS-163118=true";
	property osgi.modules.includes = "site-initializer-testray";
	property portal.release = "false";
	property portal.upstream = "true";
	property release.feature.flags.disable.DISABLE_PRIVATE_LAYOUTS = "true";
	property testray.main.component.name = "Site Initializer Testray";

	var caseName = "Case 1";
	var caseType = "Case Type 1";
	var componentName = "Component 1";
	var projectName = "Project 1";
	var teamName = "Team 1";
	var testSiteName = "Testray 2";

	setUp {
		task ("Set up instance and sign in") {
			TestCase.setUpPortalInstance();

			User.firstLoginPG();
		}

		task ("Create a new Testray Site") {
			HeadlessSite.addSite(
				siteName = ${testSiteName},
				templateName = "Testray",
				templateType = "Site Initializer");
		}

		task ("create project, team, component, case, requirement via API") {
			JSONTestray.addProject(
				projectDescription = "Project Description 1",
				projectName = ${projectName});

			JSONTestray.addTeam(
				projectName = ${projectName},
				teamName = ${teamName});

			JSONTestray.addComponent(
				componentName = ${componentName},
				projectName = ${projectName},
				teamName = ${teamName});

			JSONTestray.addCaseType(caseType = ${caseType});

			JSONTestray.addCases(
				caseName = ${caseName},
				caseType = ${caseType},
				componentName = ${componentName},
				description = "Description 1",
				priority = 5,
				projectName = ${projectName},
				steps = "Steps 1");

			JSONTestray.addRequirement(
				componentName = ${componentName},
				linkTitle = "The front page of the internet",
				projectName = ${projectName},
				requirementName = "Requirement 1");

			JSONTestray.addRequirement(
				componentName = ${componentName},
				linkTitle = "Page of the internet 2",
				linkURL = "https://reddit.com",
				projectName = ${projectName},
				requirementName = "Requirement 2");
		}

		task ("Go to the Requirements tab") {
			ApplicationsMenu.gotoSite(site = ${testSiteName});

			Testray.changePagination(quantity = 150);

			Testray.clickTableEntry(tableEntry = ${projectName});

			Navigator.gotoNavTab(navTab = "Cases");

			Testray.clickTableEntry(tableEntry = ${caseName});

			Navigator.gotoNavTab(navTab = "Requirements");
		}

		task ("add a case to the requirement") {
			Testray.selectCheckboxOptions(
				buttonText = "Select Requirements",
				checkboxNameList = "Requirement 1,Requirement 2",
				linkText = "Link Requirements");
		}
	}

	tearDown {
		task ("Delete case type and project via API, Testray Site and Client Extensions") {
			JSONTestrayAPI.deleteObjectViaAPI(
				object = "casetypes",
				objectName = ${caseType});

			JSONTestrayAPI.deleteProject(projectName = ${projectName});

			Testray.testrayApisTearDown();
		}

		var testLiferayVirtualInstance = PropsUtil.get("test.liferay.virtual.instance");

		if (${testLiferayVirtualInstance} == "true") {
			PortalInstances.tearDownCP();
		}
	}

	@description = "Story ID: LPS-173530 | Test Summary: Verify if as an Administrator I should be able to filter case requirements by link"
	@priority = 3
	test AdminCanFilterCaseRequirementsByLink {
		property test.liferay.virtual.instance = "false";
		property test.run.type = "single";

		task ("When the user filter a case requirement by link") {
			TestrayFilter.clickFilter();

			Type(
				key_fieldLabel = "Link",
				locator1 = "TextInput#GENERIC_TEXT_INPUT",
				value1 = "google");

			Button.click(button = "Apply");
		}

		task ("Then can see the filters apply in the list by link") {
			TestrayRequirements.verifyRequirement(
				linkTitle = "The front page of the internet",
				requirementComponent = "Component 1",
				requirementSummary = "Requirement 1",
				requirementTeam = "Team 1");

			Testray.viewTableEntryListNotPresent(listEntry = "Requirement 2");
		}
	}

	@description = "Story ID: LPS-173530 | Test Summary: Verify if as an Administrator I should be able to filter case requirements by summary"
	@priority = 3
	test AdminCanFilterCaseRequirementsBySummary {
		property test.liferay.virtual.instance = "false";
		property test.run.type = "single";

		task ("When the user filter a case requirement by summary") {
			TestrayFilter.clickFilter();

			Type(
				key_fieldLabel = "Summary",
				locator1 = "TextInput#GENERIC_TEXT_INPUT",
				value1 = "Requirement 1");

			Button.click(button = "Apply");
		}

		task ("Then can see the filters apply in the list by summary") {
			Testray.viewTableEntryList(listEntry = "Requirement 1");

			Testray.viewTableEntryListNotPresent(listEntry = "Requirement 2");
		}
	}

	@description = "Story ID: LPS-173530 | Test Summary: Verify if as an Administrator I should be able to filter case requirements using multiple filtering options"
	@priority = 4
	test AdminCanFilterCaseRequirementsWithMultipleOptions {
		property test.liferay.virtual.instance = "false";
		property test.run.type = "single";

		task ("When the user filter a case requirement with multiple options") {
			TestrayFilter.clickFilter();

			Type(
				key_fieldLabel = "Summary",
				locator1 = "TextInput#GENERIC_TEXT_INPUT",
				value1 = "Requirement 1");

			Type(
				key_fieldLabel = "Link",
				locator1 = "TextInput#GENERIC_TEXT_INPUT",
				value1 = "google");

			Button.click(button = "Apply");
		}

		task ("Then can see the multiple options in the table and do not see the requirement 2") {
			TestrayRequirements.verifyRequirement(
				linkTitle = "The front page of the internet",
				requirementComponent = "Component 1",
				requirementSummary = "Requirement 1",
				requirementTeam = "Team 1");

			Testray.viewTableEntryListNotPresent(listEntry = "Requirement 2");
		}
	}

	@description = "Story ID: LPS-173530 | Test Summary: Verify if as an Administrator I should not see case requirements that don't exist when I try filtering for them"
	@priority = 3
	test AdminCanFilterForNonexistentCaseRequirements {
		property test.liferay.virtual.instance = "false";
		property test.run.type = "single";

		task ("When the user filters a case requirement by its non-existent name") {
			TestrayFilter.clickFilter();

			Type(
				key_fieldLabel = "Summary",
				locator1 = "TextInput#GENERIC_TEXT_INPUT",
				value1 = "Requirement 3");

			Button.click(button = "Apply");
		}

		task ("Then can see message no results found") {
			TestrayFilter.assertNoResultsFound();
		}
	}

	@description = "Story ID: LPS-173530 | Test Summary: Verify if as an Administrator I should be able to filter the filter options"
	@priority = 2
	test AdminCanFilterOptionsToFilter {
		property test.liferay.virtual.instance = "false";
		property test.run.type = "single";

		task ("When the user search field called link") {
			TestrayFilter.clickFilter();

			TestrayFilter.searchFilter(value = "Link");
		}

		task ("Then can see the field called link and do not see the field key") {
			AssertElementPresent(
				key_fieldLabel = "Link",
				locator1 = "TextInput#GENERIC_TEXT_INPUT");

			AssertElementNotPresent(
				key_fieldLabel = "Key",
				locator1 = "TextInput#GENERIC_TEXT_INPUT");
		}
	}

}