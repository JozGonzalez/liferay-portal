@component-name = "portal-commerce"
definition {

	property custom.properties = "jsonws.web.service.paths.excludes=${line.separator}company.security.strangers.verify=false${line.separator}feature.flag.COMMERCE-10890=true";
	property portal.release = "true";
	property portal.upstream = "true";
	property release.feature.flags.disable.DISABLE_PRIVATE_LAYOUTS = "true";
	property testray.main.component.name = "Order Management";

	setUp {
		CommerceConfiguration.commerceSetUp(minium = "true");
	}

	tearDown {
		CommerceConfiguration.commerceTearDown();
	}

	@description = "This is a test case for COMMERCE-11449. Supplier account type can be created and a user linked to that account can successfully log in"
	@priority = 5
	test CanCreateSupplierTypeAccountAndALinkedUser {
		property portal.acceptance = "true";

		task ("Given the admin navigates to Minium site -> Account Management") {
			ApplicationsMenu.gotoSite(site = "Minium");

			CommerceNavigator.gotoMiniumSidebarItem(menuItem = "Account Management");
		}

		task ("When the admin proceeds to create a supplier account") {
			CommerceAccountManagement.addAccount(
				accountName = "Supplier Account",
				accountType = "Supplier");
		}

		task ("Then the account is successfully created") {
			Click(locator1 = "Icon#ANGLE_LEFT_BACK");

			LexiconTable.viewTableEntryInfo(
				index = 5,
				tableEntry = "Supplier",
				tableEntryInfo = "Supplier");
		}

		task ("When the admin creates a user for that account") {
			JSONAccountEntryUser.addAccountEntryUser(
				accountEntryName = "Supplier Account",
				userEmailAddress = "supplieruser@liferay.com",
				userFirstName = "Supplier",
				userLastName = "User",
				userScreenName = "supplieruser");

			JSONUser.setFirstPassword(
				agreeToTermsAndAnswerReminderQuery = "true",
				requireReset = "false",
				userEmailAddress = "supplieruser@liferay.com");

			JSONUser.addUserToSite(
				groupName = "Minium",
				userEmailAddress = "supplieruser@liferay.com");
		}

		task ("And the user proceeds to log in") {
			User.logoutPG();

			CommerceLogin.miniumLogin(
				password = "test",
				urlAppend = "web/minium",
				userEmailAddress = "supplieruser@liferay.com");
		}

		task ("Then the user is successfully logged in") {
			AssertElementPresent(
				key_userName = "Supplier User",
				locator1 = "CommerceAccelerators#MINIUM_SIDEBAR_USER_NAME");
		}
	}

	@description = "This is a test case for COMMERCE-11824. Supplier account can be linked to a catalog from UI."
	@priority = 5
	test CanLinkSupplierAccountToCatalogViaUI {
		property portal.acceptance = "true";

		task ("Given an active business and 2 active supplier accounts are created") {
			JSONAccountEntry.addAccountEntry(
				accountEntryName = "Commerce Account",
				accountEntryType = "Business");

			for (var num : list "1,2") {
				JSONAccountEntry.addAccountEntry(
					accountEntryName = "Supplier Account ${num}",
					accountEntryType = "Supplier");
			}
		}

		task ("And one inactive supplier account is created") {
			JSONAccountEntry.addAccountEntry(
				accountEntryName = "Supplier Account 3",
				accountEntryStatus = "Inactive",
				accountEntryType = "Supplier");
		}

		task ("When admin goes to the Minium catalog") {
			CommerceCatalog.openCatalogsAdmin();

			CommerceNavigator.gotoEntry(entryName = "Minium");
		}

		task ("Then can view 'Link Catalog to a Supplier' field is null by default and only Supplier Accounts are listed on the dropdown") {
			AssertTextEquals(
				key_label = "Link Catalog to a Supplier",
				locator1 = "CommerceEntry#ANY_AUTOCOMPLETE_CONTAINS_TEXT_FIELD",
				value1 = "");

			Click(
				key_label = "Link Catalog to a Supplier",
				locator1 = "CommerceEntry#ANY_AUTOCOMPLETE_CONTAINS_TEXT_FIELD");

			for (var accountName : list "Supplier Account 1,Supplier Account 2") {
				AssertElementPresent(
					key_categoryName = ${accountName},
					locator1 = "CommerceEntry#COMMERCE_CATEGORY_AUTOCOMPLETE_SPECIFIC");
			}
		}

		task ("And Business or Deactivated Accounts are not listed on the dropdown") {
			for (var accountName : list "Supplier Account 3,Commerce Account") {
				AssertElementNotPresent(
					key_categoryName = ${accountName},
					locator1 = "CommerceEntry#COMMERCE_CATEGORY_AUTOCOMPLETE_SPECIFIC");
			}
		}

		task ("When admin selects Supplier Account 1 for the catalog") {
			Click(
				key_label = "Link Catalog to a Supplier",
				locator1 = "CommerceEntry#ANY_AUTOCOMPLETE_CONTAINS_TEXT_FIELD");

			Click(
				key_categoryName = "Supplier Account 1",
				locator1 = "CommerceEntry#COMMERCE_CATEGORY_AUTOCOMPLETE_SPECIFIC");

			Button.clickSave();
		}

		task ("Then Supplier Account 1 is linked successfully to the catalog") {
			CommerceUtilities.checkSuccessAlertAppearsAndDisappears();

			CommerceNavigator.gotoEntry(entryName = "Minium");

			AssertTextEquals(
				key_label = "Link Catalog to a Supplier",
				locator1 = "CommerceEntry#ANY_AUTOCOMPLETE_CONTAINS_TEXT_FIELD",
				value1 = "Supplier Account 1");
		}
	}

	@description = "This is a test case for COMMERCE-10923. Supplier account can be linked to a channel from UI."
	@priority = 5
	test CanLinkSupplierAccountToChannelViaUI {
		property portal.acceptance = "true";

		task ("Given an active business and 2 active supplier accounts are created") {
			JSONAccountEntry.addAccountEntry(
				accountEntryName = "Commerce Account",
				accountEntryType = "Business");

			for (var num : list "1,2") {
				JSONAccountEntry.addAccountEntry(
					accountEntryName = "Supplier Account ${num}",
					accountEntryType = "Supplier");
			}
		}

		task ("And one inactive supplier account is created") {
			JSONAccountEntry.addAccountEntry(
				accountEntryName = "Supplier Account 3",
				accountEntryStatus = "Inactive",
				accountEntryType = "Supplier");
		}

		task ("When admin goes to the Minium Portal channel") {
			CommerceChannels.openChannelsAdmin();

			CommerceNavigator.gotoEntry(entryName = "Minium Portal");
		}

		task ("Then can view 'Link Channel to a Supplier' field is null by default and only Supplier Accounts are listed on the dropdown") {
			AssertElementPresent(
				entryName = "",
				key_fieldLabel = "Link Channel to a Supplier",
				key_index = 1,
				locator1 = "CommerceEntry#LABEL_GENERIC_SELECT_ENTRY_POSITION");

			for (var accountNameWithIndex : list "Supplier Account 1:2,Supplier Account 2:3") {
				var accountName = StringUtil.extractFirst(${accountNameWithIndex}, ":");
				var index = StringUtil.extractLast(${accountNameWithIndex}, ":");

				AssertElementPresent(
					entryName = ${accountName},
					key_fieldLabel = "Link Channel to a Supplier",
					key_index = ${index},
					locator1 = "CommerceEntry#LABEL_GENERIC_SELECT_ENTRY_POSITION");
			}
		}

		task ("And Business or Deactivated Accounts are not listed on the dropdown") {
			for (var index : list "1,2,3,4,5") {
				AssertElementNotPresent(
					entryName = "Commerce Account",
					key_fieldLabel = "Link Channel to a Supplier",
					key_index = ${index},
					locator1 = "CommerceEntry#LABEL_GENERIC_SELECT_ENTRY_POSITION");
			}

			for (var index : list "1,2,3,4,5") {
				AssertElementNotPresent(
					entryName = "Supplier Account 3",
					key_fieldLabel = "Link Channel to a Supplier",
					key_index = ${index},
					locator1 = "CommerceEntry#LABEL_GENERIC_SELECT_ENTRY_POSITION");
			}
		}

		task ("When admin selects Supplier Account 1 for the channel") {
			Select(
				key_fieldLabel = "Link Channel to a Supplier",
				locator1 = "Select#GENERIC_SELECT_FIELD",
				value1 = "Supplier Account 1");

			Button.clickSave();
		}

		task ("Then Supplier Account 1 is linked successfully to the channel") {
			CommerceUtilities.checkSuccessAlertAppearsAndDisappears();

			AssertElementPresent(
				key_fieldLabel = "Link Channel to a Supplier",
				locator1 = "Select#GENERIC_SELECT_FIELD",
				value1 = "Supplier Account 1");
		}
	}

	@description = "This is a test for COMMERCE-11608. Other account roles like a buyer who belongs to a supplier account cannot manage the channel page"
	@priority = 4
	test CannotManageChannelWhenUsersInOtherAccountRoles {
		property portal.acceptance = "false";

		task ("Given a Supplier account with a buyer user") {
			JSONAccountEntry.addAccountEntry(
				accountEntryName = "Commerce Supplier",
				accountEntryType = "Supplier");

			JSONAccountEntryUser.addAccountEntryUser(
				accountEntryName = "Commerce Supplier",
				userEmailAddress = "buyer@liferay.com",
				userFirstName = "Buyer",
				userLastName = "User",
				userScreenName = "buyeruser");

			JSONUser.setFirstPassword(
				agreeToTermsAndAnswerReminderQuery = "true",
				requireReset = "false",
				userEmailAddress = "buyer@liferay.com");

			JSONAccountRole.associateUser(
				accountEntryName = "Commerce Supplier",
				accountRole = "Buyer",
				userEmailAddress = "buyer@liferay.com");
		}

		task ("And a channel is created and the supplier account is linked to it") {
			var accountEntryId = JSONAccountEntryAPI._getAccountEntryId(accountEntryName = "Commerce Supplier");

			CommerceJSONChannelsAPI._addChannel(
				accountId = ${accountEntryId},
				channelName = "Supplier Channel",
				channelType = "site",
				currencyCode = "USD");
		}

		task ("When the buyer logs in") {
			User.logoutAndLoginPG(
				userLoginEmailAddress = "buyer@liferay.com",
				userLoginFullName = "Buyer User");
		}

		task ("Then the buyer cannot reach the Channels Page on the Admin Menu") {
			CommerceChannels.openChannelsAdmin();

			AssertElementPresent(
				locator1 = "Message#ERROR",
				value1 = "You do not have the roles required to access this portlet.");
		}
	}

	@description = "This is a test for COMMERCE-11865. As a supplier account user, I cannot access the channel panel after removing the permissions for Access in Control Panel for Channel"
	@priority = 4
	test CannotSupplierUserAccessChannelPanelAfterRemovingPermissions {
		property portal.acceptance = "false";

		task ("Given a Supplier account with a supplier user") {
			JSONAccountEntry.addAccountEntry(
				accountEntryName = "Commerce Supplier",
				accountEntryType = "Supplier");

			JSONAccountEntryUser.addAccountEntryUser(
				accountEntryName = "Commerce Supplier",
				userEmailAddress = "supplieruser@liferay.com",
				userFirstName = "Supplier",
				userLastName = "User",
				userScreenName = "supplieruser");

			JSONUser.setFirstPassword(
				agreeToTermsAndAnswerReminderQuery = "true",
				requireReset = "false",
				userEmailAddress = "supplieruser@liferay.com");

			JSONAccountRole.associateUser(
				accountEntryName = "Commerce Supplier",
				accountRole = "Account Supplier",
				userEmailAddress = "supplieruser@liferay.com");
		}

		task ("And a channel is created and the supplier account is linked to it") {
			var accountEntryId = JSONAccountEntryAPI._getAccountEntryId(accountEntryName = "Commerce Supplier");

			CommerceJSONChannelsAPI._addChannel(
				accountId = ${accountEntryId},
				channelName = "Supplier Channel",
				channelType = "site",
				currencyCode = "USD");
		}

		task ("When admin removes the supplier's permission with Access in Control Panel for Channel") {
			Permissions.removePermissionViaAPI(
				resourceAction = "ACCESS_IN_CONTROL_PANEL",
				resourceName = "com_liferay_commerce_channel_web_internal_portlet_CommerceChannelsPortlet",
				roleTitle = "Supplier");
		}

		task ("And the supplier user logs in") {
			User.logoutAndLoginPG(
				userLoginEmailAddress = "supplieruser@liferay.com",
				userLoginFullName = "Supplier User");
		}

		task ("Then the supplier user cannot view the Channel panel") {
			ApplicationsMenuHelper.openApplicationsMenu();

			ApplicationsMenu.gotoPanel(panel = "Commerce");

			AssertElementNotPresent(
				key_category = "Store Management",
				key_portlet = "Channels",
				locator1 = "ApplicationsMenu#PORTLET");
		}

		task ("And cannot navigate to the Channels panel by URL either") {
			CommerceChannels.openChannelsAdmin();

			AssertElementPresent(
				locator1 = "Message#ERROR",
				value1 = "You do not have the roles required to access this portlet.");
		}
	}

	@description = "This is a test for COMMERCE-11866. If a linked supplier account to a channel is removed, the supplier account user cannot view any channels"
	@priority = 4
	test CannotSupplierUserViewAUnlinkedChannel {
		property portal.acceptance = "false";

		task ("Given a Supplier account with a supplier user") {
			JSONAccountEntry.addAccountEntry(
				accountEntryName = "Commerce Supplier",
				accountEntryType = "Supplier");

			JSONAccountEntryUser.addAccountEntryUser(
				accountEntryName = "Commerce Supplier",
				userEmailAddress = "supplieruser@liferay.com",
				userFirstName = "Supplier",
				userLastName = "User",
				userScreenName = "supplieruser");

			JSONUser.setFirstPassword(
				agreeToTermsAndAnswerReminderQuery = "true",
				requireReset = "false",
				userEmailAddress = "supplieruser@liferay.com");

			JSONAccountRole.associateUser(
				accountEntryName = "Commerce Supplier",
				accountRole = "Account Supplier",
				userEmailAddress = "supplieruser@liferay.com");
		}

		task ("And a channel is created and the supplier account is linked to it") {
			var accountEntryId = JSONAccountEntryAPI._getAccountEntryId(accountEntryName = "Commerce Supplier");

			CommerceJSONChannelsAPI._addChannel(
				accountId = ${accountEntryId},
				channelName = "Supplier Channel",
				channelType = "site",
				currencyCode = "USD");
		}

		task ("When the supplier user logs in") {
			User.logoutAndLoginPG(
				userLoginEmailAddress = "supplieruser@liferay.com",
				userLoginFullName = "Supplier User");
		}

		task ("Then the supplier user can view the Supplier Channel that the account is linked to") {
			CommerceChannels.openChannelsAdmin();

			AssertElementPresent(
				key_entryName = "Supplier Channel",
				locator1 = "CommerceNavigation#ENTRY_NAME");
		}

		task ("When admin unlinks the Supplier account for Supplier Channel") {
			User.logoutAndLoginPG(
				userLoginEmailAddress = "test@liferay.com",
				userLoginFullName = "Test Test");

			CommerceChannels.openChannelsAdmin();

			CommerceNavigator.gotoEntry(entryName = "Supplier Channel");

			Select(
				key_fieldLabel = "Link Channel to a Supplier",
				locator1 = "Select#GENERIC_SELECT_FIELD",
				value1 = "");

			Button.clickSave();
		}

		task ("Then the supplier cannot see any channel entries displayed") {
			User.logoutAndLoginPG(
				userLoginEmailAddress = "supplieruser@liferay.com",
				userLoginFullName = "Supplier User");

			CommerceChannels.openChannelsAdmin();

			AssertTextEquals(
				locator1 = "Message#EMPTY_STATE_INFO",
				value1 = "No Results Found");
		}
	}

	@description = "This is a test for COMMERCE-11516. When a supplier account is deleted after being linked to a catalog, the account will not be shown as linked to the catalog"
	@priority = 3
	test CannotViewDeletedSupplierAccountLinkedToCatalogViaAPI {
		property portal.acceptance = "false";

		task ("Given a 'Supplier' type of account is created") {
			CommerceJSONAccountsAPI._addCommerceAccount(
				accountName = "Supplier Account",
				accountType = "Supplier",
				externalReferenceCode = "Supplier Account ERC");
		}

		task ("And a catalog is created and the supplier account is linked to it") {
			var accountEntryId = JSONAccountEntryAPI._getAccountEntryId(accountEntryName = "Supplier Account");

			CommerceJSONCatalogsAPI._addCatalog(
				accountEntryId = ${accountEntryId},
				catalogName = "Test Catalog");
		}

		task ("When the supplier account is deleted") {
			CommerceJSONAccountsAPI._deleteAllCommerceAccounts();
		}

		task ("Then the supplier account will not be shown as linked to the catalog") {
			var accountId = CommerceJSONCatalogsAPI._getCommerceAccountIdByCatalogId(catalogName = "Test Catalog");

			TestUtils.assertNotEquals(
				actual = ${accountId},
				expected = ${accountEntryId});
		}
	}

	@description = "This is a test for COMMERCE-11516. When a supplier account is deleted after being linked to a channel, the account will not be shown as linked to the channel"
	@priority = 3
	test CannotViewDeletedSupplierAccountLinkedToChannelViaAPI {
		property portal.acceptance = "false";

		task ("Given a 'Supplier' type of account is created") {
			CommerceJSONAccountsAPI._addCommerceAccount(
				accountName = "Supplier Account",
				accountType = "Supplier",
				externalReferenceCode = "Supplier Account ERC");
		}

		task ("And a channel is created and the supplier account is linked to it") {
			var accountEntryId = JSONAccountEntryAPI._getAccountEntryId(accountEntryName = "Supplier Account");

			CommerceJSONChannelsAPI._addChannel(
				accountId = ${accountEntryId},
				channelName = "Test Channel",
				channelType = "site",
				currencyCode = "USD");
		}

		task ("When the supplier account is deleted") {
			CommerceJSONAccountsAPI._deleteAllCommerceAccounts();
		}

		task ("Then the supplier account will not be shown as linked to the channel") {
			var accountId = CommerceJSONChannelsAPI._getCommerceAccountIdByChannelId(channelName = "Test Channel");

			TestUtils.assertNotEquals(
				actual = ${accountId},
				expected = ${accountEntryId});
		}
	}

	@description = "COMMERCE-11610. As a supplier account user, I can add and edit products in a catalog and delete them from the catalog the account is linked to."
	@priority = 5
	test CanSupplierManageOnlyLinkedCatalogProducts {
		property portal.acceptance = "true";

		task ("Given a Supplier account with a supplier user") {
			JSONAccountEntry.addAccountEntry(
				accountEntryName = "Commerce Supplier",
				accountEntryType = "Supplier");

			JSONAccountEntryUser.addAccountEntryUser(
				accountEntryName = "Commerce Supplier",
				userEmailAddress = "supplier@liferay.com",
				userFirstName = "Supplier",
				userLastName = "User",
				userScreenName = "supplieruser");

			JSONUser.setFirstPassword(
				agreeToTermsAndAnswerReminderQuery = "true",
				requireReset = "false",
				userEmailAddress = "supplier@liferay.com");

			JSONAccountRole.associateUser(
				accountEntryName = "Commerce Supplier",
				accountRole = "Account Supplier",
				userEmailAddress = "supplier@liferay.com");
		}

		task ("And Supplier account is linked to Minium catalog") {
			var accountId = CommerceJSONAccountsAPI.getAccountFieldByAccountName(
				accountName = "Commerce Supplier",
				field = "id");

			CommerceJSONCatalogsAPI._patchCommerceCatalog(
				accountId = ${accountId},
				catalogName = "Minium");
		}

		task ("And a new Catalog is added") {
			CommerceJSONCatalogsAPI._addCatalog(catalogName = "New Catalog");
		}

		task ("And the Supplier account is linked to Minium Portal Channel") {
			CommerceChannels.openChannelsAdmin();

			CommerceNavigator.gotoEntry(entryName = "Minium Portal");

			Select(
				key_fieldLabel = "Link Channel to a Supplier",
				locator1 = "Select#GENERIC_SELECT_FIELD",
				value1 = "Commerce Supplier");

			Button.clickSave();
		}

		task ("When the supplier user logs in") {
			User.logoutAndLoginPG(
				userLoginEmailAddress = "supplier@liferay.com",
				userLoginFullName = "Supplier User");
		}

		task ("Then can add a new product in Minium Catalog") {
			CommerceProducts.openProductsAdmin();

			CPCommerceCatalog.newProductsEntry(
				productCatalog = "Minium",
				productName = "Simple T-Shirt",
				productType = "Simple");

			AssertSelectedLabel(
				key_fieldLabel = "Catalog",
				locator1 = "Select#GENERIC_SELECT_FIELD",
				value1 = "Minium");
		}

		task ("And can edit the product") {
			CommerceEntry.addProductDescription(
				productFullDescription = "This is a Full Description",
				productShortDescription = "This is a Short Description",
				publishProduct = "true");

			CommerceEntry.gotoMenuTab(menuTab = "Options");

			CommerceProductOptions.createProductOption(key_optionName = "New Option");

			CommerceEntry.gotoMenuTab(menuTab = "SKUs");

			CommerceNavigator.gotoEntry(entryName = "default");

			CommerceEntry.editSkuPricingAndInventory(
				basePrice = "20.00",
				italyQuantity = 20,
				publish = "true");

			CommerceEntry.gotoMenuTab(menuTab = "Product Relations");

			CommerceRelations.addAnyProductRelation(
				key_product = "U-Joint",
				key_relation = "Up-Sell Product");

			CommerceEntry.gotoMenuTab(menuTab = "Subscription");

			CommerceProducts.configureSubscription(
				subscriptionExactDay = 1,
				subscriptionLength = 5,
				subscriptionMode = "Exact Day of Month",
				subscriptionName = "Delivery Subscription",
				subscriptionType = "Month");

			CommerceEntry.gotoMenuTab(menuTab = "Visibility");

			AssertElementNotPresent(
				key_name = "Channels",
				locator1 = "ToggleSwitch#ENABLE_SPECIFIC_TOGGLE_SWITCH");
		}

		task ("But cannot add a new product in a different Catalog") {
			Click(locator1 = "Icon#BACK");

			Click(locator1 = "Button#PLUS");

			Click(
				key_menuItem = "Simple",
				locator1 = "MenuItem#DROPDOWN_MENU_ITEM");

			WaitForElementPresent(locator1 = "IFrame#MODAL_BODY");

			SelectFrame.selectFrameNoLoading(locator1 = "IFrame#MODAL_BODY");

			Type(
				locator1 = "CommerceEntry#NAME_FIELD",
				value1 = "New Product");

			for (var entryName : list "Master,New Catalog") {
				Type(
					key_label = "Catalog",
					locator1 = "CommerceEntry#ANY_AUTOCOMPLETE_TEXT_FIELD",
					value1 = ${entryName});

				AssertElementPresent(
					key_text = "No items were found.",
					locator1 = "CP2Utils#ANY_TEXT");
			}

			Click(locator1 = "Icon#CLOSE");

			SelectFrame.selectFrameTop();
		}

		task ("When the supplier user deletes the product") {
			CommerceNavigator.searchEntry(entryName = "Simple T-Shirt");

			CommerceProducts.deleteProductsEntry(key_value = "Simple T-Shirt");
		}

		task ("Then can verify that Simple Product is deleted successfully") {
			CommerceNavigator.searchEntry(entryName = "Simple T-Shirt");

			AssertElementNotPresent(
				key_entryName = "Simple T-Shirt",
				locator1 = "CommerceNavigation#ENTRY_NAME");
		}
	}

	@description = "COMMERCE-11604 | As a supplier account user, I want to be able to manage the catalogs the account is linked to"
	@priority = 5
	test CanSupplierUserManageCatalogs {
		property portal.acceptance = "true";

		task ("Given a Supplier account with a supplier user") {
			JSONAccountEntry.addAccountEntry(
				accountEntryName = "Commerce Supplier",
				accountEntryType = "Supplier");

			JSONAccountEntryUser.addAccountEntryUser(
				accountEntryName = "Commerce Supplier",
				userEmailAddress = "supplier@liferay.com",
				userFirstName = "Supplier",
				userLastName = "User",
				userScreenName = "supplieruser");

			JSONUser.setFirstPassword(
				agreeToTermsAndAnswerReminderQuery = "true",
				requireReset = "false",
				userEmailAddress = "supplier@liferay.com");

			JSONAccountRole.associateUser(
				accountEntryName = "Commerce Supplier",
				accountRole = "Account Supplier",
				userEmailAddress = "supplier@liferay.com");
		}

		task ("And Supplier account is linked to a catalog") {
			var accountId = CommerceJSONAccountsAPI.getAccountFieldByAccountName(
				accountName = "Commerce Supplier",
				field = "id");

			CommerceJSONCatalogsAPI._patchCommerceCatalog(
				accountId = ${accountId},
				catalogName = "Minium");
		}

		task ("And a price list and a promotion are created") {
			CommerceJSONPriceListsAndPromotionsAPI._addPriceList(
				catalogName = "Minium",
				currencyCode = "USD",
				priceListName = "Test Price List");

			CommerceJSONPriceListsAndPromotionsAPI._addPromotion(
				catalogName = "Minium",
				currencyCode = "USD",
				promotionName = "Test Promotion");
		}

		task ("When the supplier user logs in") {
			User.logoutAndLoginPG(
				userLoginEmailAddress = "supplier@liferay.com",
				userLoginFullName = "Supplier User");
		}

		task ("Then the supplier user can view only the catalog the account is linked to") {
			CommerceCatalog.openCatalogsAdmin();

			AssertElementPresent(
				key_entryName = "Minium",
				locator1 = "CommerceNavigation#ENTRY_NAME");

			AssertElementNotPresent(
				key_entryName = "Master",
				locator1 = "CommerceNavigation#ENTRY_NAME");
		}

		task ("And can add but cannot delete catalogs") {
			AssertElementPresent(locator1 = "Button#PLUS");

			AssertElementNotPresent(
				key_value = "Minium",
				locator1 = "CommerceEntry#BODY_VERTICAL_ELLIPSIS");
		}

		task ("When the supplier user navigates to a catalog details") {
			Click(
				key_firstColumnValue = "Minium",
				key_secondColumnValue = "USD",
				key_table = "",
				locator1 = "CommerceEntry#TABLE_ASSERT_TWO_COLUMNS_EDIT_BUTTON");
		}

		task ("Then the user is able to edit the catalog fields") {
			CommerceCatalog.editCatalogEntry(
				catalogBasePriceList = "Test Price List",
				catalogBasePromotion = "Test Promotion",
				catalogCurrency = "Euro",
				catalogLanguage = "Catalan (Spain)",
				catalogNewName = "Minium 2");

			Click(
				key_firstColumnValue = "Minium 2",
				key_secondColumnValue = "EUR",
				key_table = "",
				locator1 = "CommerceEntry#TABLE_ASSERT_TWO_COLUMNS_EDIT_BUTTON");

			CommerceEntry.viewCatalogsEntry(
				catalogBasePriceList = "Test Price List",
				catalogBasePromotion = "Test Promotion",
				catalogCurrency = "Euro",
				catalogLanguage = "Catalan (Spain)",
				catalogName = "Minium 2");
		}
	}

	@description = "This is a test for COMMERCE-11607. As a supplier account user, I want to be able to manage the channel the account is linked to"
	@priority = 5
	test CanSupplierUserManageChannel {
		property portal.acceptance = "true";

		task ("Given a Supplier account with a supplier user") {
			JSONAccountEntry.addAccountEntry(
				accountEntryName = "Commerce Supplier",
				accountEntryType = "Supplier");

			JSONAccountEntryUser.addAccountEntryUser(
				accountEntryName = "Commerce Supplier",
				userEmailAddress = "supplieruser@liferay.com",
				userFirstName = "Supplier",
				userLastName = "User",
				userScreenName = "supplieruser");

			JSONUser.setFirstPassword(
				agreeToTermsAndAnswerReminderQuery = "true",
				requireReset = "false",
				userEmailAddress = "supplieruser@liferay.com");

			JSONAccountRole.associateUser(
				accountEntryName = "Commerce Supplier",
				accountRole = "Account Supplier",
				userEmailAddress = "supplieruser@liferay.com");
		}

		task ("And two channels are created and the supplier account is linked to Supplier Channel 1") {
			var accountEntryId = JSONAccountEntryAPI._getAccountEntryId(accountEntryName = "Commerce Supplier");

			CommerceJSONChannelsAPI._addChannel(
				accountId = ${accountEntryId},
				channelName = "Supplier Channel 1",
				channelType = "site",
				currencyCode = "USD");

			CommerceJSONChannelsAPI._addChannel(
				channelName = "Supplier Channel 2",
				channelType = "site",
				currencyCode = "USD");
		}

		task ("When admin links the Supplier account for Supplier Channel 2") {
			CommerceChannels.openChannelsAdmin();

			CommerceNavigator.gotoEntry(entryName = "Supplier Channel 2");

			Select(
				key_fieldLabel = "Link Channel to a Supplier",
				locator1 = "Select#GENERIC_SELECT_FIELD",
				value1 = "Commerce Supplier");

			Button.clickSave();
		}

		task ("Then an error alert 'A supplier account can be linked only to one channel' is shown") {
			Alert.viewErrorMessage(errorMessage = "Error:A supplier account can be linked only to one channel.");
		}

		task ("When the supplier user logs in") {
			User.logoutAndLoginPG(
				userLoginEmailAddress = "supplieruser@liferay.com",
				userLoginFullName = "Supplier User");
		}

		task ("Then the supplier user can view only the Supplier Channel 1 that the account is linked to") {
			CommerceChannels.openChannelsAdmin();

			AssertElementPresent(
				key_entryName = "Supplier Channel 1",
				locator1 = "CommerceNavigation#ENTRY_NAME");

			for (var channelName : list "Supplier Channel 2,Minium Portal") {
				AssertElementNotPresent(
					key_entryName = ${channelName},
					locator1 = "CommerceNavigation#ENTRY_NAME");
			}
		}

		task ("And cannot add/delete channels, only can edit the channel") {
			AssertElementNotPresent(locator1 = "Button#PLUS");

			AssertElementNotPresent(
				key_value = "Supplier Channel 1",
				locator1 = "CommerceEntry#BODY_VERTICAL_ELLIPSIS");

			AssertElementPresent(
				key_firstColumnValue = "Supplier Channel 1",
				key_secondColumnValue = "Site",
				key_table = "",
				locator1 = "CommerceEntry#TABLE_ASSERT_TWO_COLUMNS_EDIT_BUTTON");
		}

		task ("When the supplier user navigates to the channel details") {
			Click(
				key_firstColumnValue = "Supplier Channel 1",
				key_secondColumnValue = "Site",
				key_table = "",
				locator1 = "CommerceEntry#TABLE_ASSERT_TWO_COLUMNS_EDIT_BUTTON");
		}

		task ("Then the user is able to edit the channel fields") {
			CommerceChannels.activatePaymentMethod(paymentMethod = "Money Order");

			AssertElementPresent(
				key_entryName = "Money Order",
				key_entryStatus = "Active",
				key_table = "Payment Methods",
				locator1 = "CommerceEntry#TABLE_ENTRY_STATUS");
		}
	}

	@description = "This is a test for COMMERCE-11868. As a supplier account user, I can manage discounts associated with my products via Price Lists and Promotions Panels"
	@priority = 5
	test CanSupplierUserManageDiscountsInPriceListsAndPromotions {
		property portal.acceptance = "true";

		task ("Given a Supplier account with a supplier user") {
			JSONAccountEntry.addAccountEntry(
				accountEntryName = "Commerce Supplier",
				accountEntryType = "Supplier");

			JSONAccountEntryUser.addAccountEntryUser(
				accountEntryName = "Commerce Supplier",
				userEmailAddress = "supplieruser@liferay.com",
				userFirstName = "Supplier",
				userLastName = "User",
				userScreenName = "supplieruser");

			JSONUser.setFirstPassword(
				agreeToTermsAndAnswerReminderQuery = "true",
				requireReset = "false",
				userEmailAddress = "supplieruser@liferay.com");

			JSONAccountRole.associateUser(
				accountEntryName = "Commerce Supplier",
				accountRole = "Account Supplier",
				userEmailAddress = "supplieruser@liferay.com");
		}

		task ("And a buyer user with a business account is created") {
			CommerceEntry.addAccountEntryUser(
				accountName = "Commerce Account",
				accountType = "Business",
				agreeToTermsAndAnswerReminderQuery = "true",
				createNewAccount = "true",
				requireReset = "false",
				userEmailAddress = "buyer@liferay.com",
				userFirstName = "Buyer",
				userLastName = "User",
				userRole = "Buyer",
				userScreenName = "buyeruser",
				userSiteMembership = "Minium");
		}

		task ("And a product belongs to another catalog is created") {
			CommerceJSONProductsAPI._addCommerceProduct(
				catalogName = "Master",
				productName = "Simple Product",
				productType = "Simple",
				sku = "SKU1001");
		}

		task ("And Supplier account is linked to Minium catalog") {
			var accountEntryId = JSONAccountEntryAPI._getAccountEntryId(accountEntryName = "Commerce Supplier");

			CommerceJSONCatalogsAPI._patchCommerceCatalog(
				accountId = ${accountEntryId},
				catalogName = "Minium");
		}

		task ("When the admin creates a discount for products with base price and promo price") {
			CommerceJSONDiscountsAPI._addDiscount(
				active = "true",
				level = "L1",
				percentageLevel1 = 10,
				productNames = "ABS Sensor,Brake Pads",
				target = "products",
				title = "Test Discount",
				usePercentage = "true");
		}

		task ("Then the buyer can view the two products with discounts that admin added before") {
			User.logoutPG();

			CommerceLogin.miniumLogin(
				password = "test",
				urlAppend = "web/minium",
				userEmailAddress = "buyer@liferay.com");

			Navigator.openWithAppendToBaseURL(urlAppend = "web/minium/p/abs-sensor");

			CommerceAcceleratorsInitializer.viewDiscountedProductDetailsPage(
				discountPercentage = 10,
				productDiscountedPrice = "45.00",
				productListPrice = "50.00");

			Navigator.openWithAppendToBaseURL(urlAppend = "web/minium/p/brake-pads");

			CommerceAcceleratorsInitializer.viewDiscountedProductDetailsPage(
				discountPercentage = 10,
				productDiscountedPrice = "17.01",
				productListPrice = "21.00",
				productPromoPrice = "18.90");
		}

		task ("When the supplier user logs in") {
			User.logoutAndLoginPG(
				userLoginEmailAddress = "supplieruser@liferay.com",
				userLoginFullName = "Supplier User");
		}

		task ("Then the supplier user cannot view the Discounts panel") {
			ApplicationsMenuHelper.openApplicationsMenu();

			ApplicationsMenu.gotoPanel(panel = "Commerce");

			AssertElementNotPresent(
				key_category = "Pricing",
				key_portlet = "Discounts",
				locator1 = "ApplicationsMenu#PORTLET");
		}

		task ("When the supplier user adds a new price entry with discount set in a new price list") {
			CommercePriceLists.openPriceListsAdmin();

			CommerceNavigator.gotoEntry(entryName = "Minium Base Price List");

			CommerceEntry.gotoMenuTab(menuTab = "Entries");

			CommerceNavigator.searchEntry(entryName = "SKU1001");

			AssertTextEquals(
				locator1 = "Message#EMPTY_STATE_INFO",
				value1 = "No Results Found");

			CommerceProducts.searchAndGoToProductEntry(entryProductName = "MIN93015");

			SelectFrame.selectFrameNoLoading(locator1 = "CommerceEntry#IFRAME_SIDE_PANEL");

			CommercePriceLists.setOverrideDiscount(overrideDiscounts = "L1-20");
		}

		task ("And the supplier user adds a new promo entry with discount set in a new promotion") {
			CommercePromotions.openPromotionsAdmin();

			CommerceNavigator.gotoEntry(entryName = "Minium Base Promotion");

			CommerceEntry.gotoMenuTab(menuTab = "Entries");

			CommerceNavigator.searchEntry(entryName = "SKU1001");

			AssertTextEquals(
				locator1 = "Message#EMPTY_STATE_INFO",
				value1 = "No Results Found");

			CommerceProducts.searchAndGoToProductEntry(entryProductName = "MIN93018");

			SelectFrame.selectFrameNoLoading(locator1 = "CommerceEntry#IFRAME_SIDE_PANEL");

			CommercePromotions.setOverrideDiscount(overrideDiscounts = "L1-20");
		}

		task ("Then the buyer can view the discounts of the products that are updated by supplier user accordingly") {
			User.logoutPG();

			CommerceLogin.miniumLogin(
				password = "test",
				urlAppend = "web/minium",
				userEmailAddress = "buyer@liferay.com");

			Navigator.openWithAppendToBaseURL(urlAppend = "web/minium/p/abs-sensor");

			CommerceAcceleratorsInitializer.viewDiscountedProductDetailsPage(
				discountPercentage = 20,
				productDiscountedPrice = "40.00",
				productListPrice = "50.00");

			Navigator.openWithAppendToBaseURL(urlAppend = "web/minium/p/brake-pads");

			CommerceAcceleratorsInitializer.viewDiscountedProductDetailsPage(
				discountPercentage = 20,
				productDiscountedPrice = "15.12",
				productListPrice = "21.00",
				productPromoPrice = "18.90");
		}
	}

	@description = "COMMERCE-11611. As a supplier account user, I cannot view the products in a catalog the account is not linked to."
	@priority = 4
	test CanSupplierViewOnlyLinkedCatalogProducts {
		property portal.acceptance = "false";

		task ("Given a Supplier account with a supplier user") {
			JSONAccountEntry.addAccountEntry(
				accountEntryName = "Commerce Supplier",
				accountEntryType = "Supplier");

			JSONAccountEntryUser.addAccountEntryUser(
				accountEntryName = "Commerce Supplier",
				userEmailAddress = "supplier@liferay.com",
				userFirstName = "Supplier",
				userLastName = "User",
				userScreenName = "supplieruser");

			JSONUser.setFirstPassword(
				agreeToTermsAndAnswerReminderQuery = "true",
				requireReset = "false",
				userEmailAddress = "supplier@liferay.com");

			JSONAccountRole.associateUser(
				accountEntryName = "Commerce Supplier",
				accountRole = "Account Supplier",
				userEmailAddress = "supplier@liferay.com");
		}

		task ("And a new Catalog with a product is added") {
			CommerceJSONCatalogsAPI._addCatalog(catalogName = "Test Catalog");

			CommerceJSONProductsAPI._addCommerceProduct(
				catalogName = "Test Catalog",
				price = 5,
				productName = "Simple Product",
				productType = "simple",
				sku = "TESTSKU");
		}

		task ("And a product is added to the Master catalog") {
			CommerceJSONProductsAPI._addCommerceProduct(
				catalogName = "Master",
				price = 25,
				productName = "New Product",
				productType = "simple",
				sku = "NEWSKU");
		}

		task ("And Supplier account is linked to Test Catalog") {
			var accountId = CommerceJSONAccountsAPI.getAccountFieldByAccountName(
				accountName = "Commerce Supplier",
				field = "id");

			CommerceJSONCatalogsAPI._patchCommerceCatalog(
				accountId = ${accountId},
				catalogName = "Test Catalog");
		}

		task ("When the supplier user logs in") {
			User.logoutAndLoginPG(
				userLoginEmailAddress = "supplier@liferay.com",
				userLoginFullName = "Supplier User");
		}

		task ("Then can view the product from Test Catalog") {
			CommerceProducts.openProductsAdmin();

			CommerceNavigator.searchEntry(entryName = "Simple Product");

			AssertElementPresent(
				key_entryName = "Simple Product",
				locator1 = "CommerceNavigation#ENTRY_NAME");
		}

		task ("But cannot view products from Minium and Master Catalogs") {
			for (var productName : list "ABS Sensor,Brake Fluid,Hoses,Torque Converters,New Product") {
				CommerceNavigator.searchEntry(entryName = ${productName});

				AssertElementNotPresent(
					key_entryName = ${productName},
					locator1 = "CommerceNavigation#ENTRY_NAME");
			}
		}
	}

}