import com.liferay.gradle.util.copy.ReplaceLeadingPathAction

apply plugin: "org.ysb33r.gradletest"

task copyGradleTestDependencies(type: Copy)

String gradleVersion = "6.9.2"

tasks.eclipse {
	ext.gradleVersion = gradleVersion
}

copyGradleTestDependencies {
	duplicatesStrategy = DuplicatesStrategy.INCLUDE

	from configurations.compileClasspath
	into jar.destinationDir
}

dependencies {
	api group: "com.github.jk1", name: "gradle-license-report", version: "1.8"
	api group: "com.gradle.publish", name: "plugin-publish-plugin", version: "0.11.0"
	api group: "com.liferay", name: "com.liferay.gradle.plugins", version: "14.0.160"
	api group: "com.liferay", name: "com.liferay.gradle.plugins.app.javadoc.builder", version: "1.2.4"
	api group: "com.liferay", name: "com.liferay.gradle.plugins.baseline", version: "7.0.0"
	api group: "com.liferay", name: "com.liferay.gradle.plugins.cache", version: "1.0.18"
	api group: "com.liferay", name: "com.liferay.gradle.plugins.change.log.builder", version: "1.1.5"
	api group: "com.liferay", name: "com.liferay.gradle.plugins.dependency.checker", version: "1.0.5"
	api group: "com.liferay", name: "com.liferay.gradle.plugins.go", version: "2.0.0"
	api group: "com.liferay", name: "com.liferay.gradle.plugins.jsdoc", version: "2.0.71"
	api group: "com.liferay", name: "com.liferay.gradle.plugins.patcher", version: "1.0.21"
	api group: "com.liferay", name: "com.liferay.gradle.plugins.poshi.runner", version: "3.0.75"
	api group: "com.liferay", name: "com.liferay.gradle.plugins.testray", version: "1.0.15"
	api group: "com.liferay", name: "com.liferay.gradle.plugins.whip", version: "1.0.9"
	api group: "gradle.plugin.com.github.maiflai", name: "gradle-scalatest", version: "0.25"
	api group: "gradle.plugin.com.github.spotbugs.snom", name: "spotbugs-gradle-plugin", version: "4.0.0"
	api group: "org.apache.aries.jmx", name: "org.apache.aries.jmx.api", version: "1.1.0"
	api group: "org.springframework.boot", name: "spring-boot-gradle-plugin", version: "2.7.1"

	compileOnly fileTree(builtBy: [rootProject.tasks.getByName("extractGradleApi" + gradleVersion.replace(".", ""))], dir: new File(rootProject.buildDir, "gradle-${gradleVersion}"))
}

gradleTest {
	dependsOn copyGradleTestDependencies
	dependsOn jar

	versions gradleVersion
}

processResources {
	into("com/liferay/gradle/plugins/defaults/dependencies") {
		from "../../../tools/sdk/dependencies/net.sourceforge.pmd/rulesets/java/standard-rules.xml"
	}
}

updateFileVersions {
	match(/classpath group: "com\.liferay", name: "com\.liferay\.gradle\.plugins\.defaults", version: "(\d.+)"/, "../../../portal-web/build-test.gradle")
}